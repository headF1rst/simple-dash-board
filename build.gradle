plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'net.ltgt.errorprone' version '4.1.0'
}

group = 'org.example'
version = '0.0.1-SNAPSHOT'
description = 'simple-dash-board'

allprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'net.ltgt.errorprone'

    java {
        sourceCompatibility = JavaVersion.VERSION_25
        targetCompatibility = JavaVersion.VERSION_25
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        // JSpecify
        implementation 'org.jspecify:jspecify:1.0.0'

        errorprone "com.google.errorprone:error_prone_core:2.38.0"
        errorprone "com.uber.nullaway:nullaway:0.12.6"
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    tasks.withType(JavaCompile).configureEach {
        options.errorprone {
            disableAllChecks = true
            option("NullAway:OnlyNullMarked", "true")
            error("NullAway")
        }
    }

}
